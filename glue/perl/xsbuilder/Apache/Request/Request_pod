=head1 NAME

Apache::Request - Methods for dealing with client request data

=head1 SYNOPSIS

    use Apache::Request;
    my $req = Apache::Request->new($r);

=head1 DESCRIPTION

C<Apache::Request> adds methods for parsing B<GET> requests and B<POST> 
requests where I<Content-type> is one of I<application/x-www-form-urlencoded>
or I<multipart/form-data>.

=head1 Apache::Request METHODS

The interface is designed to mimic CGI.pm 's routines for parsing
query parameters. The main differences are 

=over 4

=item * C<Apache::Request::new> takes an environment-specific
object as (second) argument.  Newer versions of CGI.pm also accept
this syntax within modperl.

=item * The query parameters are stored in APR::Table derived objects,
and are therefore parsed using case-insensitive keys.

=item * The query string is always parsed first, even for POST requests.

=back

=head2 C<new>  

creates a new C<Apache::Request> object with an environment object $r:

    my $req = Apache::Request->new($r);

With mod_perl2, the environment object must be an C<Apache::RequestRec>
object.  All methods from the environment class are inherited.

The following attributes are optional:

=over 4

=item C<POST_MAX>, C<MAX_BODY>

Limit the size of POST data (in bytes).

=item C<DISABLE_UPLOADS>

Disable file uploads.

=item C<TEMP_DIR>

Sets the directory where upload files are spooled.  On a *nix-like
that supports I<link(2)>, the TEMP_DIR should be located on the same
file system as the final destination file:

 use Apache::Upload;
 my $req = Apache::Request->new($r, TEMP_DIR => "/home/httpd/tmp");
 my $upload = $req->upload('file');
 $upload->link("/home/user/myfile") || warn "link failed: $!";

For more details on C<link>, see the L<Apache::Upload> manpage.

=item C<HOOK_DATA>

Extra configuration info passed as the fourth argument 
to an upload hook.  See the description for the next item, 
C<UPLOAD_HOOK>.

=item C<UPLOAD_HOOK>

Sets up a callback to run whenever file upload data is read. This
can be used to provide an upload progress meter during file uploads.
Apache will automatically continue writing the original data to
$upload->fh after the hook exits.

  my $transparent_hook = sub {
    my ($upload, $data, $data_len, $hook_data) = @_;
    warn "$hook_data: got $data_len bytes for " . $upload->name;
  };

  my $req = Apache::Request->new($r, 
                                 HOOK_DATA => "Note",
                                 UPLOAD_HOOK => $transparent_hook,
                                );

=back


=head2 C<instance($r)> [DEPRECATED]

The default (and only) behavior of I<Apache::Request> is to intelligently
cache B<POST> data for the duration of the request.  Thus there is no longer
the need for a separate C<instance()> method as existed in I<Apache::Request>
for Apache 1.3 - all B<POST> data is always available from each and every 
I<Apache::Request> object created during the request's lifetime.

However an C<instance()> method is aliased to C<new()> in this release
to ease the pain of porting from 1.X to 2.X.

=head2 C<param([$name])>

Get the request parameters (using case-insensitive keys) by
mimicing the OO interface of C<CGI::param>.

    # similar to CGI.pm

    my $foo_value   = $req->param('foo');
    my @foo_values  = $req->param('foo');
    my @param_names = $req->param;

    # the following differ slightly from CGI.pm

    # returns ref to Apache::Request::Table object representing 
    # all (args + body) params
    my $table = $req->param;

In list context, or when invoked with no arguments as 
C<$req-&gt;param()>, C<param> always induces libapreq2 
to read and parse all remaining data in the request body.
However, C<scalar $req->param("foo")> is lazy: libapreq2 
will only read and parse more data if

    1) no "foo" param appears in the query string arguments, AND
    2) no "foo" param appears in the previously parsed POST data.

In this circumstance libapreq2 will read and parse additional
blocks of the incoming request body until it has found the
the "foo" param or parsing is completed.

Note: modifications to the C<scalar $req-&gt;param()> table only
affect the returned table object (the underlying C apr_table_t is 
generated by apreq_params()).  They do not affect the actual 
request data, and will not be seen by other libapreq2 applications.


XXX EXCEPTIONS XXX


=head2 C<parms>, C<params> [DEPRECATED]

The functionality of these functions is assumed by C<param>,
so they are no longer necessary.  Aliases to C<param> are
provided in this release for backwards compatibility,
however they may be removed from a future release.


=head2 C<args([$name])>

Returns an I<Apache::Request::Table> object containing the query-string 
parameters of the I<Apache::Request> object.

   my $args = $req->args;

An optional name parameter can be passed to return the query string
parameter associated with the given name:

   my $arg = $req->args($name);

More generally, C<args()> follows the same pattern as C<param()>
with respect to its return values and argument list.  The main difference
is that modifications to the C<scalar $req-&gt;args()> table affect
the underlying apr_table_t attribute in apreq_request_t, so their impact
will be noticed by all libapreq2 applications during this request.


=head2 C<body([$name])>

Returns an I<Apache::Request::Table> object containing the POST data 
parameters of the I<Apache::Request> object.

   my $body = $req->body;

An optional name parameter can be passed to return the POST data
parameter associated with the given name:

   my $param = $req->body($name);

More generally, C<body()> follows the same pattern as C<param()>
with respect to its return values and argument list.  The main difference
is that modifications to the C<scalar $req-&gt;body()> table affect
the underlying apr_table_t attribute in apreq_request_t, so their impact 
will be noticed by all libapreq2 applications during this request.


=head2 C<upload([$name])> (requires Apache::Upload)

With no arguments, this returns an I<Apache::Upload::Table> object in 
scalar context, or the names of all I<Apache::Upload> objects in
list context.

An optional name parameter can be passed to return the I<Apache::Upload>
object associated with the given name:

    my $upload = $req->upload($name);

Note: modifications to the C<scalar $req-&gt;upload()> table only
affect the returned table object (the underlying C apr_table_t is 
generated by apreq_uploads()).  They do not affect the actual request 
data, and will not be seen by other libapreq2 applications.


=head2 C<args_status()>

Reports the final I<APR> status code of the query-string parser.
APR_SUCCESS on success, error otherwise.

=head2 C<body_status()>

Reports the current I<APR> status code of the POST data parser.
APR_SUCCESS when parser has completed, APR_INCOMPLETE if parser
has more data to parse, error otherwise.

=head2 C<param_status()>

In scalar context, this returns C<args_status> if there was
an error during the query-string parse, otherwise this returns
C<body_status>.  In list context this returns the list 
C<(args_status, body_status)>.

=head1 SUBCLASSING Apache::Request

If the instances of your subclass are hash references then you can actually
inherit from Apache::Request as long as the Apache::Request object is stored in
an attribute called "r" or "_r". (The Apache::Request class effectively does the
delegation for you automagically, as long as it knows where to find the
Apache::Request object to delegate to.)  For example:

	package MySubClass;
	use Apache::Request;
	our @ISA = qw(Apache::Request);
	sub new {
		my($class, @args) = @_;
		return bless { r => Apache::Request->new(@args) }, $class;
	}


=head1 PORTING from 1.X to 2.X

This is the complete list of changes to existing methods 
from Apache::Request 1.X.  These issues need to be 
addressed when porting 1.X apps to the new 2.X API.

=over 4

=item Apache::Upload is now a separate module.  Applications
      requiring the upload API must C<use Apache::Upload> in 2.X.
      This is easily addressed by preloading the modules during 
      server startup.

=item You must use the C<Apache::Request::Table> API via 
      C<scalar $req-&gt;args> or C<scalar $req-&gt;body> 
      to assign new parameters to the request.  You may 
      no longer use the two-argument method calls; e.g.

        $req->param("foo" => "bar"); # NO: usage error in 2.X
        $req->args->{foo} = "bar";   # OK: assign to args table
        $req->body->add(foo => "bar");  # OK: add to body table

        $req->param->add(foo => "bar"); # NO: this is an expensive noop, because
                                        # the param table is generated
                                        # by overlaying $req->args and $req->body.

        my $params = $req->param;
        $params->set(foo => "bar"); # OK: sets "foo" entry in $params, which is a local
                                    # (args + body) table. Neither $req->args, $req->body,
                                    # nor future calls to $req->param, are affected by 
                                    # mods to $params.


=item C<instance()> is now identical to C<new()>, and is now deprecated.  It 
      may be removed from a future 2.X release.

=item C<param> includes the functionality of C<parms()> and C<params()>, so they
      are now deprecated and may be removed from a future 2.X release.

=back


=head1 SEE ALSO

L<Apache::Cookie>, L<Apache::Upload>, L<APR::Table>

=head1 CREDITS

This interface is based on the original pure Perl version by Lincoln Stein.

=head1 MISSING DOCS

$req->config, Apache::Request::Table

